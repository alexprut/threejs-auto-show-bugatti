<html>
<head>
    <title>Bugatti Veyron - Auto Show</title>
    <link rel="shortcut icon" href="img/favicon.png">
    <link rel="stylesheet" type="text/css" href="bower_components/colpick-jQuery-Color-Picker/css/colpick.css">
    <link rel="stylesheet" type="text/css" href="css/style.css">
    <script src="bower_components/threejs/build/three.js"></script>
    <script src="bower_components/jquery/dist/jquery.min.js"></script>
    <script src="bower_components/colpick-jQuery-Color-Picker/js/colpick.js"></script>

    <script src="bower_components/threejs/examples/js/loaders/BinaryLoader.js"></script>
    <script src="bower_components/threejs/examples/js/loaders/TGALoader.js"></script>

    <script src="bower_components/stats.js/build/stats.min.js"></script>
    <script src="bower_components/threejs/examples/js/libs/dat.gui.min.js"></script>

    <script src="bower_components/threejs/examples/js/shaders/CopyShader.js"></script>

    <script src="bower_components/threejs/examples/js/postprocessing/EffectComposer.js"></script>
    <script src="bower_components/threejs/examples/js/postprocessing/RenderPass.js"></script>
    <script src="bower_components/threejs/examples/js/postprocessing/ShaderPass.js"></script>
    <script src="bower_components/threejs/examples/js/postprocessing/MaskPass.js"></script>

    <script src="bower_components/threejs/examples/js/controls/OrbitControls.js"></script>
</head>
<body>
    <script type="text/x-glsl" id="vertex-shader">
        uniform vec3 lightPosition[4];

        varying vec2 vUv;
        varying vec3 lVector[4];
        varying vec4 lPosition[4];
        varying vec3 vNormal;
        varying vec3 eyePosition;

        void main() {
            gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);

            #ifdef HAS_UV
                vUv = uv;
            #else
                vUv = vec2(0, 0);
            #endif

            vNormal = normalMatrix * normal;
            eyePosition = (modelViewMatrix * vec4(position, 1.0)).xyz;

            vec4 modelViewPosition = modelViewMatrix * vec4(position, 1.0);

            for (int i = 0; i < 4; i++) {
                lPosition[i] = viewMatrix * vec4(lightPosition[i], 1.0);
                lVector[i] = lPosition[i].xyz - modelViewPosition.xyz;
            }
        }
    </script>
    <script type="text/x-glsl" id="fragment-shader">
        uniform int type;
        uniform vec3 rho;
        uniform vec3 ambient;
        uniform sampler2D map;
        uniform vec3 lightPower;
        uniform vec2 textureRepeat;
        uniform int isMap;
        uniform sampler2D normalMap;
        uniform vec3 diffuseColor;
        uniform vec3 specColor;
        uniform float roughness;
        uniform vec3 lightPosition[4];

        const float PI = 3.14159265359;

        varying vec2 vUv;
        varying vec3 lVector[4];
        varying vec4 lPosition[4];
        varying vec3 vNormal;
        varying vec3 eyePosition;

        #ifdef HAS_NORMAL_MAP
        #extension GL_OES_standard_derivatives : enable

        vec3 perturbNormal2Arb(vec3 eye_pos, vec3 defaultNormal) {
            vec3 q0 = dFdx(eye_pos.xyz);
            vec3 q1 = dFdy(eye_pos.xyz);
            vec2 st0 = dFdx(vUv.st);
            vec2 st1 = dFdy(vUv.st);

            vec3 S = normalize(q0 * st1.t - q1 * st0.t);
            vec3 T = normalize(-q0 * st1.s + q1 * st0.s);
            vec3 N = normalize(defaultNormal);

            vec3 mapN = texture2D(normalMap, vUv * textureRepeat).xyz * 2.0 - 1.0;
            mat3 tsn = mat3(S, T, N);
            return normalize(tsn * mapN);
        }
        #endif

        vec3 beta(vec3 lightPower, vec3 lVector) {
            return (lightPower / (4.0  * PI * length(lVector)));
        }

        // Cook-Torrance approximation
        float G(float LdotH) {
            return (1.0 / pow(LdotH, 2.0));
        }

        // Fresnel reflection, Schlick approximation
        vec3 F(float LdotH, vec3 specColor) {
            return specColor + (vec3(1.0) - specColor) * pow(1.0 - LdotH, 5.0);
        }

        // Trowbridge-Reitz
        float D(float NdotH, float roughness) {
            float A = pow(roughness, 2.0);
            float B = PI * pow(pow(NdotH, 2.0) * (A - 1.0) + 1.0, 2.0);
            return A / B;
        }

        void main() {
            vec3 outRadiance = vec3(0.0);
            vec3 currentRho = rho;
            vec3 normal = vNormal;

            #ifdef HAS_NORMAL_MAP
                normal = perturbNormal2Arb(eyePosition, vNormal);
            #endif

            #ifdef HAS_MAP
                currentRho = texture2D(map, vUv * textureRepeat).xyz;
            #endif

            if (type == 1) {
                // Phong
                // http://www.mathematik.uni-marburg.de/~thormae/lectures/graphics1/code/WebGLShaderLightMat/ShaderLightMat.html
                for (int i = 0; i < 4; i++) {
                    vec3 vertPos = vec3(lPosition[i]) / lPosition[i].w;
                    vec3 lightDir = normalize(lightPosition[i] - vertPos);
                    vec3 reflectDir = reflect(-lightDir, vNormal);
                    vec3 viewDir = normalize(-vertPos);

                    float lambertian = max(dot(lightDir, normal), 0.0);
                    float specular = 0.0;

                    if(lambertian > 0.0) {
                        float specAngle = max(dot(reflectDir, viewDir), 0.0);
                        specular = pow(specAngle, 4.0);

                        // according to the rendering equation we would need to multiply
                        // with the the "lambertian", but this has little visual effect
                        specular *= lambertian;
                    }

                    outRadiance += (lambertian * diffuseColor + specular * specColor);
                }
            } else if (type == 2) {
                // Microfacet
                for (int i = 0; i < 4; i++) {
                    vec3 n = normalize(vNormal);
                    vec3 v = normalize(-eyePosition);
                    vec3 l = normalize(lVector[i]);
                    vec3 h = normalize(l+v);
                    float NdotH = max(0.000000000001, dot(n, h));
                    float VdotH = max(0.000000000001, dot(v, h));
                    float NdotV = max(0.000000000001, dot(n, v));
                    float NdotL = max(0.000000000001, dot(n, l));
                    vec3 specular = F(VdotH, specColor) * G(VdotH) * D(NdotH, roughness) / 4.0;
                    outRadiance += beta(lightPower, lVector[i]) * NdotL * specular;
                }
            } else {
                // Lambert (default)
                for (int i = 0; i < 4; i++) {
                    float dotProduct = dot(normal, normalize(lVector[i]));
                    float pointLightWeighting = max(dotProduct, 0.0);
                    outRadiance += (beta(lightPower, lVector[i]) * pointLightWeighting * currentRho / PI);
                }
            }

            outRadiance += ambient;
            gl_FragColor = vec4(outRadiance, 1.0);
        }
    </script>
    <script type="text/x-glsl" id="vertex-default-texture">
        varying vec2 vUv;

        void main() {
            vUv = uv;
            gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
        }
    </script>
    <script type="text/x-glsl" id="fragment-vignette">
        uniform float offset;
        uniform float darkness;
        uniform sampler2D tDiffuse;

        varying vec2 vUv;

        void main() {
            vec4 texel = texture2D(tDiffuse, vUv);
            vec2 uv = (vUv - vec2(0.5) ) * vec2(offset);
            gl_FragColor = vec4(mix(texel.rgb, vec3(1.0 - darkness), dot(uv, uv)), texel.a);
        }
    </script>
    <div id="panel">
        <div id="logo">
            <img src="img/bugatti-logo.png" alt=""/>
        </div>
    </div>
    <div id="controls">
        <button class="btn icon-color"></button>
    </div>
    <script src="js/main.js"></script>
</body>
</html>
